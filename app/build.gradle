apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'playpublisher'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "com.yoavst.quickapps"
        minSdkVersion 19
        targetSdkVersion 20
        versionCode 22
        versionName "2.1"
        applicationVariants.all { variant ->
            if (variant.name == "release") {
                def file = variant.outputFile
                if (project.hasProperty('OUTPUT_DIR') && new File((String) OUTPUT_DIR).exists())
                    parent = (String) OUTPUT_DIR
                else
                // the module folder
                    parent = (String) project(':app').projectDir.absolutePath
                variant.outputFile = new File(
                        parent,
                        (String) file.name.replace(
                                ".apk",

                                // alter this string to change output file name
                                "-" + defaultConfig.versionName + ".apk"
                        )
                )
                println("Apk stored on: " + variant.outputFile.getPath())
            }
        }
    }
    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    lintOptions {
        abortOnError false
    }
    buildTypes {
        debug {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

apt {
    arguments {
        androidManifestFile variant.processResources.manifestFile
        resourcePackageName "com.yoavst.quickapps"
    }
}

publisher {
    applicationName "Quick Circle Apps"
    packageName "com.yoavst.quickapps"
    apkFile =  file("app-release-" + android.defaultConfig.versionName + ".apk")
    authStore = new File(".store")
    secretFile = file(PLAY_STORE_KEY)
    productType 'production' // production, alpha, beta
}

dependencies {
    apt "org.androidannotations:androidannotations:3.0.1"
    compile "org.androidannotations:androidannotations-api:3.0.1"
    compile fileTree(dir: 'libs', include: ['*.jar'])
    provided fileTree(dir: 'provided', include: ['*.jar'])
    compile 'com.joanzapata.android:android-iconify:1.0.6'
    compile ('com.actionbarsherlock:viewpagerindicator:2.4.1') {
        exclude module: 'support-v4'
    }
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'org.scribe:scribe:1.3.5'
    compile 'com.github.johnkil.android-robototextview:robototextview:2.1.0'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'com.makeramen:roundedimageview:1.3.0'
}
